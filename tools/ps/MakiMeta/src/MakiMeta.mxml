<?xml version="1.0" encoding="utf-8"?>
<csxs:CSExtension xmlns:fx="http://ns.adobe.com/mxml/2009"
				  xmlns:s="library://ns.adobe.com/flex/spark"
				  xmlns:csxs="com.adobe.csxs.core.*"
				  close="csextension1_closeHandler(event)"
				  creationComplete="csextension1_creationCompleteHandler(event)"
				  showStatusBar="false">
	
	<fx:Script>
		<![CDATA[
			import com.adobe.csawlib.photoshop.Photoshop;
			import com.adobe.csxs.core.CSInterface;
			import com.adobe.csxs.core.CSXSInterface;
			import com.adobe.photoshop.*;
			import com.adobe.xmp.core.*;
			
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			private const SELECT:int = Photoshop.app.stringIDToTypeID("select");
			private const MAKE:int = Photoshop.app.stringIDToTypeID("make");
			private const DELETE:int = Photoshop.app.stringIDToTypeID("delete");
			
			private const NULL:int = Photoshop.app.stringIDToTypeID("null");
			private const LAYER:int = Photoshop.app.stringIDToTypeID("layer");
			private const ORDINAL:int = Photoshop.app.stringIDToTypeID("ordinal");
			
			private var maki:Namespace = new Namespace("maki", "http://makiengine.com/");
			private var currentLayer:Layer = null;
			
			protected function csextension1_creationCompleteHandler(event:FlexEvent):void
			{
				CSInterface.instance.autoThemeColorChange = true;
				
				// EXTRA SPECIAL NOTE OF PARAMOUNT IMPORTANCE:
				// CSXSInterface is deprecated, but evalScript("PhotoshopRegisterEvent"... doesn't work
				// with the newer CSInterface!
				try {
					CSXSInterface.instance.evalScript("PhotoshopRegisterEvent", SELECT.toString());
					CSXSInterface.instance.evalScript("PhotoshopRegisterEvent", MAKE.toString());
					CSXSInterface.instance.evalScript("PhotoshopRegisterEvent", DELETE.toString());
					ExternalInterface.addCallback("PhotoshopCallback" + extensionId, photoshopCallback);
				} catch(ex:*) {
					Alert.show(ex);
				}
				
				if(Photoshop.app.activeDocument != null) {
					currentLayer = Photoshop.app.activeDocument.activeLayer;
					layerName.text = currentLayer.name;
				} else {
					layerName.text = "<none>";
				}
				updateTextBox();
			}
			
			protected function csextension1_closeHandler(event:Event):void {}
			protected function applyButton_clickHandler(event:MouseEvent):void
			{
				try {
					saveData(currentLayer);
				} catch(ex:*) {
					// Layer may not exist anymore, etc
					currentLayer = null;
					layerName.text = "<none>";
					metaTextArea.text = "";
				}
			}
			
			protected function metaTextArea_keyFocusChangeHandler(event:FocusEvent):void
			{
				// Prevent tab presses from switching focus
				event.preventDefault();
				event.target.insertText("\t");
			}
			
			private function photoshopCallback(eventID:Number, descID:Number):void
			{
				var actionDesc:ActionDescriptor = new ActionDescriptor();
				var ref:ActionReference;
				var cls:int;
				
				switch(eventID) {
					case SELECT:
						try {
							actionDesc.fromID(descID);
							ref = actionDesc.getReference(NULL);
							cls = ref.getDesiredClass();
							if(cls == LAYER) {
								layerSelectionChanged();
							}
						} catch(ex:*) {
							currentLayer = null;
							layerName.text = "<none>";
							metaTextArea.text = "";
						}
						break;
					case MAKE:
						try {
							actionDesc.fromID(descID);
							ref = actionDesc.getReference(NULL);
							cls = ref.getDesiredClass();
							if(cls == LAYER) {
								layerSelectionChanged();
							}
						} catch(ex:*) {
							currentLayer = null;
							layerName.text = "<none>";
							metaTextArea.text = "";
						}
						break;
					case DELETE:
						actionDesc.fromID(descID);
						ref = actionDesc.getReference(NULL);
						cls = ref.getDesiredClass();
						if(cls == LAYER) {
							if(ref.getEnumeratedType() == ORDINAL) {
								// This means we are deleting the currently selected layer
								currentLayer = null;
								layerName.text = "<none>";
								metaTextArea.text = "";
							}
							try {
								layerSelectionChanged();
							} catch(ex:*) {
								currentLayer = null;
								layerName.text = "<none>";
								metaTextArea.text = "";
							}
						}
						break;
					default:
						break;
				}
			}
			
			private function updateTextBox():void
			{
				var xmpMeta:XmpMetadata = null;
				try {
					xmpMeta = currentLayer.xmpMetadata;						
				} catch(ex:*) {
					currentLayer = null;
					layerName.text = "<none>";
					metaTextArea.text = "";
					return;
				}
				
				if(xmpMeta != null) {
					var xmp:XMPMeta = new XMPMeta(xmpMeta.rawData);
					try {
						var prop:XMPProperty = xmp.maki::data;
						if(prop.exists()) {
							var decoded:String = prop.value;
							decoded = decoded.replace(/%0d/g, '\r');
							decoded = decoded.replace(/%0a/g, '\n');
							decoded = decoded.replace(/%09/g, '\t');
							decoded = decoded.replace(/%25/g, '%');
							metaTextArea.text = decoded;
							return;
						}
					} catch(ex:*) {
						// Catch type coersion errors (sometimes we get an XMPStruct instead
						// of an XMPProperty)
					}
				}
				metaTextArea.text = "";
			}
			
			private function saveData(layer:Layer):void
			{
				var raw:String = null;
				try {
					raw = layer.xmpMetadata.rawData;	
				} catch(ex:*) {
					// Layer probably doesn't exist anymore
					return;
				}
				
				var xmp:XMPMeta = null;
				if(raw != null) {
					xmp = new XMPMeta(raw);
				} else {
					xmp = new XMPMeta();
				}
				
				var encoded:String = metaTextArea.text;
				encoded = encoded.replace(/%/g, '%25');
				encoded = encoded.replace(/\r/g, '%0d');
				encoded = encoded.replace(/\n/g, '%0a');
				encoded = encoded.replace(/\t/g, '%09');
				xmp.maki::data = encoded;
				
				layer.xmpMetadata.rawData = new String(xmp.serializeToBuffer());
			}
			
			private function layerSelectionChanged():void
			{
				// Get a ref to the now selected layer
				if(Photoshop.app.activeDocument != null) {
					currentLayer = Photoshop.app.activeDocument.activeLayer;
					layerName.text = currentLayer.name;
				}
				
				// Show contents of current layer in textbox
				updateTextBox();
			}
			
		]]>
	</fx:Script>
	<s:Group height="100%" width="100%">
		<s:Button id="applyButton" right="16" bottom="2" label="Save" click="applyButton_clickHandler(event)"/>
		<s:TextArea id="metaTextArea" left="3" right="3" top="3" bottom="26"
					fontFamily="Courier New" horizontalScrollPolicy="auto"
					keyFocusChange="metaTextArea_keyFocusChangeHandler(event)" lineBreak="explicit"
					verticalScrollPolicy="auto"/>
		<s:Label id="layerName" left="10" bottom="6" text="&lt;none&gt;"/>
	</s:Group>
</csxs:CSExtension>